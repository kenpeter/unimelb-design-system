window.UOMinjectHeader = ->
  # IE helper
  # The navigator contains browser version, name, cookie, languages, etc, use it wisely.
  bodyclass = 'ie ie8' if /(MSIE 8.0)/g.test(navigator.userAgent)
  bodyclass = 'ie ie9' if /(MSIE 9.0)/g.test(navigator.userAgent)
  bodyclass = 'ie ie10' if /(MSIE 10.0)/g.test(navigator.userAgent)

  # If it is ie 8, 9, 10, css style is different.
  unless document.body.hasClass('ie') or (typeof bodyclass == 'undefined')
    document.body.addClass(bodyclass)

  # Logo links to
  defaultlink = 'https://www.unimelb.edu.au'

  ###
  http://stackoverflow.com/questions/14457084/getting-ruby-environment-variables-from-rake
  So in command line, you can set ASSET_ENV=development, then here it can pick it up.
 
  assethost is like all the images, javascirpt files, etc??
  ###
  if "<%=ENV['ASSET_ENV']%>" == 'development'
    assethost = '/assets/shared'
  else
    assethost = '//uom-design-system.s3.amazonaws.com/shared'

  if document.countSelector('.page-header') == 0

    ###
    Create page wrapper if it doesn't already exist
      <body>
      <uomcontent>
    ###
    parent = document.querySelector('.uomcontent')
    unless parent
      parent = document.createElement('div')
      parent.addClass('uomcontent')
      document.body.appendChild parent

      ###
      For example, document.body.childNodes: for childNodes
      0: text (like separator)
      1: <div class="uomcontent"></div>
      2: text (like separator)
      3: <div class="whatever"></div>
      
      body --> <div class="uomcomtent"></div> make sure everything is inside uomcontent
      ###
      for n in document.body.childNodes
        if n and n != parent
          document.body.removeChild(n)
          parent.appendChild(n)

    page = document.querySelector('.page-inner')
    unless page
      page = document.createElement('div')
      page.addClass('page-inner')

    ###
    div.uomcontent
      div.page-header
        header
          uom-logo (svg)
          div.page-header-navigation
            div.page-local-history
              breadcrumb
        div.page-header-tools
          search
          login
          menu
      div.page-inner
        div.role=main
          content
        div.page-footer
        div.hidden
          all svg images
      div.modal__blanket (i.e. overlay)
      div.modal__dialog for uom-login
      div.role="navigation"
      div.sitemap-label
      div.role="sitemap"
        the long sitemap within menu
    script: google tag manager
    script: hidden lp submit div????
    ###
    # Create header if it doesn't already exist
    header = document.querySelector('.page-header')
    unless header
      # Create header and move local breadcrumb
      header = document.createElement('div')
      header.addClass('page-header')

      ###
        Normally this is the home page. which means no white strip on the very top
        Otherwise, has white strip
      ###
      if document.countSelector('.page-inner > .floating') == 1
        # Landing page header
        header.innerHTML = """
        <a class="page-header-logo" href="#{defaultlink}">
          <svg width="100" height="100" viewBox="0 0 140 140" aria-labelledby="aria-uom-title" role="img">
            <title id="aria-uom-title">The University of Melbourne Logo</title>
            <image xlink:href="#{assethost}/logo.svg" src="#{assethost}/logo.png" alt="The University of Melbourne Logo" width="140" height="140" preserveAspectRatio="xMaxYMin meet"/>
          </svg>
        </a>
        """
        header.addClass('floating')
        if document.querySelector('.page-inner > .floating').hasClass('reverse')
          header.addClass('reverse')

        # Add any customisations
        for n in document.querySelector('.floating').childNodes
          header.appendChild n

      else
        ###
          div.page-local-history
            <a class="root">.....
          basically, it is building the root uom home breadcrumb url.
        ###
        if document.countSelector('.page-local-history .root') == 0
          rootlink = """
          <a href="https://unimelb.edu.au/" title="The University of Melbourne">The University of Melbourne</a>
          """
        else
          rootlink = ""

        # General header
        header.innerHTML = """
        <header>
          <a class="page-header-logo" href="#{defaultlink}">
            <svg width="100" height="100" viewBox="0 0 140 140" aria-labelledby="aria-uom-title" role="img">
              <title id="aria-uom-title">The University of Melbourne Logo</title>
              <image xlink:href="#{assethost}/logo.svg" src="#{assethost}/logo.png" alt="The University of Melbourne Logo" width="140" height="140" preserveAspectRatio="xMaxYMin meet"/>
            </svg>
          </a>
          <div class="page-header-navigation">
            #{rootlink}
          </div>
        </header>
        """

      ###
        div.uomcontent ===> parent
          div.page-header
          div.page-inner
      ###
      parent.insertBefore(header, page)

      ###
        div.page-header-navigation
          div.page-local-history
      ###
      local = document.querySelector('.page-local-history')
      if local
        navparent = document.querySelector('.page-header-navigation')
        local.parentNode.removeChild(local)
        if navparent
          if rootlink != ""
            sep = document.createElement "span"
            sep.innerHTML = "/"
            navparent.appendChild(sep)

          navparent.appendChild(local)

    # Header tools
    tools = document.querySelector('.page-header-tools')
    unless tools
      tools = document.createElement "div"
      tools.addClass('page-header-tools')
      if document.countSelector('[role="main"].no-login') == 0
        tools.innerHTML = """
              <a class="page-header-icon" href="#sitemap" title="Search"><svg role="img"><use xlink:href="#icon-search"/></svg> Search</a><!--
              --><a class="page-header-icon" href="#sitemap" title="Login" data-modal-target="uom-login"><svg role="img"><use xlink:href="#icon-user" /></svg> Portals</a><!--
              --><a class="page-header-icon" href="#sitemap" title="Menu"><svg role="img"><use xlink:href="#icon-menu"/></svg> Menu</a>
        """
      else
        # Search and menu only, why??
        tools.innerHTML = """
              <a class="page-header-icon" href="#sitemap" title="Search"><svg role="img"><use xlink:href="#icon-search" /></svg> Search</a><!--
              --><a class="page-header-icon" href="#sitemap" title="Menu"><svg role="img"><use xlink:href="#icon-menu" /></svg> Menu</a>
        """

      header.appendChild tools

      window.addEventListener "scroll", ->
        if /(Firefox)/g.test(navigator.userAgent) or /(Trident)/g.test(navigator.userAgent)
          outer = document.querySelector('html')
        else
          outer = document.body

        # So here it is when scroll with scroll bar, the header will become smaller and stick to top.
        if outer.scrollTop > 40
          header.addClass 'fixed'
        else
          header.removeClass 'fixed'

    # header is div.page-header, with role='banner'
    header.setAttribute('role', 'banner')

    main = document.querySelector('[role="main"]')
    unless main
      main = document.createElement('div')
      main.setAttribute('role', 'main')
    else
      main.parentNode.removeChild(main)

    footer = document.querySelector('.page-footer')

    ###
      div.page-inner
        div.main
        div.page-footer
    ###
    page.insertBefore(main, footer)

    # sitemap === local navigation
    sitemap = document.querySelector('[role="sitemap"]')

    ###
      div.uomcontent
        div.page-header
        div.page-inner
        div.sitemap
  
      Basically, move everything else into div.main
    ###
    for n in parent.childNodes
      if n and n != page and n != sitemap and n != header
        parent.removeChild(n)
        main.appendChild(n)

    # https://developer.mozilla.org/en-US/docs/Web/API/Node/appendChild
    # page already exists, so move it and insert at the end of div.uomcontent
    parent.appendChild page

    # Set up login modal and attach to page
    login = document.querySelector('.page-login')
    unless login
      if document.countSelector('[role="main"].no-login') == 0
        login = document.createElement "div"
        login.addClass('modal__dialog')
        login.addClass('page-login')
        login.id = 'uom-login'
        login.innerHTML = """
                <a class="modal__close">Close</a>
                <h2 class="title">Please Choose</h2>
                <div class="half">
                  <a class="button-fill" href="https://my.unimelb.edu.au/studentportal/faces/home">
                    <svg role="img"><use xlink:href="#icon-students" /></svg>
                    <h2>Current Students</h2>
                    <p>Go to my.unimelb</p>
                  </a>
                  <a class="button-fill" href="https://staff.unimelb.edu.au">
                    <svg role="img"><use xlink:href="#icon-user" /></svg>
                    <h2>Staff Members</h2>
                    <p>Go to the staff hub</p>
                  </a>
                </div>
        """
        parent.appendChild login
